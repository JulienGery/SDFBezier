#version 450

#define EPSILON 1e-5

layout (binding = 0) uniform ParameterUBO {
    vec2 P_0, p1, p2, p3;
    vec4 bisector;
    int maxIndex, width, height, curveIndex;
} ubo;

layout(std140, binding = 2) readonly buffer ParticleSSBOIn
{
    vec4 roots[][3];
};

layout(std140, binding = 3) buffer currentClosesetPoints
{
    vec4 result[];
    //layout: distance, inside, root, curveIndex
};

float crossZ(const in vec2 d, const in vec2 v)
{
    const vec2 a = normalize(d); 
    const vec2 b = normalize(v);
    return (a.x * b.y - a.y * b.x);
}

vec2 Bezier(const float t)
{
    return ubo.P_0 +
    2.f * t * ubo.p1 +
    t * t * ubo.p2;
}

vec2 DBezier(const float t)
{
    return 2.f * ubo.p1 +
		    2.f * t * ubo.p2;
}

float distSquared(const vec2 A, const vec2 B )
{
    const vec2 C = A - B;
    return dot( C, C );
}

float crossN(const vec2 d, const vec2 v)
{
    return d.x * v.y - d.y * v.x;
}


layout (local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

void main()
{
    const uint index = gl_GlobalInvocationID.x;
    if(index < ubo.maxIndex)
    {
        const float x = index % ubo.width;
        const float y = (index - x) / ubo.width;
        const vec2 point = vec2(x, y);

        for(uint i = 0; i < 3; i++)
        {
            const vec2 root = roots[index][i].xy;
            if(abs(root.y) < 1e-05)
            {
                float real = root.x;    
                real = clamp(real, 0.f, 1.f);
                const vec2 Candidate = Bezier(real);

                const vec2 jsp = point - Candidate;

                const float dis = distance(Candidate, point);
                const float inside = crossZ(DBezier(real), jsp);

                if(dis <= result[index].x && 0. < real && real < 1.)
                    result[index] = vec4(dis, inside, real, ubo.curveIndex);
                 else if(dis <= result[index].x && crossN(ubo.bisector.xy, point - Bezier(0.)) <= 0. && crossN(ubo.bisector.zw, point - Bezier(1.)) >= 0.)
                    result[index] = vec4(dis, inside, real, ubo.curveIndex);
            }
        }
    }
}